// Simple linear continuous inventory control problem

domain inventory {

	types {
		item : object;
	};

	pvariables {

		PURCHASE-COST(item) : { non-fluent, real, default = 1.0 };
		PRICE(item)         : { non-fluent, real, default = 2.0 };
		MAX-ITEMS           : { non-fluent, int, default = 10 };
		MIN-DEMAND(item) : { non-fluent, int, default = 0 }; 		
		MED-DEMAND(item) : { non-fluent, int, default = 2 };		
		MAX-DEMAND(item) : { non-fluent, int, default = 4 };
		P-MIN : { non-fluent, real, default = 0.25 };
		P-MED : { non-fluent, real, default = 0.6 };
		
		is-min(item) : { interm-fluent, bool };
		is-med(item) : { interm-fluent, bool };
		raw-demand(item) : { interm-fluent, int }; 
			
		stock(item) : { state-fluent, int, default = 0 };
	
		order(item) : { action-fluent, int, default = 0 };
	};

	cpfs {
		is-min(?i) = Bernoulli(P-MIN);
		is-med(?i) = Bernoulli(P-MED);		
		raw-demand(?i) = floor[is-min(?i) * MIN-DEMAND(?i) 
								+ (1 - is-min(?i)) * is-med(?i) * MED-DEMAND(?i) 
								+ (1 - is-min(?i)) * (1 - is-med(?i)) * MAX-DEMAND(?i)];
		stock'(?i) = max[0, stock(?i) + order(?i) - raw-demand(?i)];
	};

	reward = (sum_{?i : item} [PRICE(?i) * (stock(?i) + order(?i) - stock'(?i)) - PURCHASE-COST(?i) * order(?i)]);

	action-preconditions {
		forall_{?i : item} [ order(?i) >= 0 ];
		(sum_{?i : item} [stock(?i) + order(?i)]) <= MAX-ITEMS;
	};
	
	state-invariants {
		forall_{?i : item} [ stock(?i) >= 0 ];
		(sum_{?i : item} stock(?i)) <= MAX-ITEMS;
	};
}