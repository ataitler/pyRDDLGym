////////////////////////////////////////////////////////////////////
// A simple continuous MDP for the classical pendulum problem.
//
// A single robotic arm hangs freely from a central joint located
// at a fixed position. Action is the torque applied on the joint.
// The goal is to raise the tip of the arm to a desired height.
//
////////////////////////////////////////////////////////////////////
domain pendulum {

    requirements = {
        reward-deterministic
    };
	
    pvariables {
	
		// math constants
		PI 			: { non-fluent, real, default = 3.14159 };		// the constant pi
		
		// physics constants
		DT 		 	: { non-fluent, real, default = 0.05 };			// change in time per decision epoch
		GRAVITY		: { non-fluent, real, default = 9.8 };			// force of gravity
		
    	// system properties
		LENGTH 		: { non-fluent, real, default = 1 }; 			// length of the arm
		MASS   		: { non-fluent, real, default = 1 };			// mass of the arm
		MAX-DTHETA  : { non-fluent, real, default = 8 };	  		// maximum speed of the arm
		MAX-TORQUE 	: { non-fluent, real, default = 2 }; 			// maximum torque
		
		// states
       	theta    	: { state-fluent, real, default = 0 };          // angle of joint
		dtheta 		: { state-fluent, real, default = 0 };			// angular velocity of joint
		
       	// actions
       	torque 		: { action-fluent, real, default = 0 };       	// torque to apply to the joint in [-2, 2]
    };

    cpfs {
	
		// calculate the new angular velocity
        dtheta' = max[min[dtheta + DT * (3 * GRAVITY / (2 * LENGTH) * sin[theta] + torque * 3 / (MASS * pow[LENGTH, 2])),
						MAX-DTHETA], -MAX-DTHETA];
		
		// update the angle
		theta' = theta + DT * dtheta';
    };

    // penalize low angle, penalize high angular velocity, and penalize torque (e.g. fuel cost)
    reward = pow[theta - div[theta + PI, 2 * PI] * 2 * PI, 2] + 0.1 * pow[dtheta, 2] + 0.001 * pow[torque, 2];

    state-invariants {
		dtheta >= -MAX-DTHETA ^ dtheta <= MAX-DTHETA;
    };

    action-preconditions {
        torque >= -MAX-TORQUE;
		torque <= MAX-TORQUE;
    };

}
